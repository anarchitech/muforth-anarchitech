loading STM32F407 example startup code

__meta
hex
{ h @ } flash
0 vector-table
{ h ! }
ram
ld target/ARM/v6-m/kernel-itc.mu4

: wait-for-rdy begin dup c@ 2 and until drop ;

: clock-init 5 ( HSEON + HSEBYP) [ RCC_CR 2 + #] tuck c! wait-for-rdy
  [ 7 ( PLLQ) #24 <<
    1 ( PLLSRC) #22 << +
    0 ( PLLP = 2, code not value!) #16 << +
    #168 ( PLLN) #6 << +
    4 ( PLLM) + #] RCC_PLLCFGR !
  1 [ RCC_CR 3 + #] tuck c! wait-for-rdy 
  2 RCC_CFGR tuck c! 
  begin dup c@ %1010 = until drop ;
 
: gpiod-init
  ff RCC_AHB1ENR !
  55000000 GPIOD_MODER ! ;

: wait for next ;
: flash-leds f000 GPIOD_ODR ! 8_0000 wait 
             0000 GPIOD_ODR ! ;
here ] ( chip-init)
  5 FLASH_ACR +! ( set latency for 168MHz @ 3.3v) clock-init gpiod-init flash-leds bug [

Reset_exc handler
   sp rp mov   #64 cells # sp sp sub
   ( chip-init) \a ip  lit
   "decafbad    \a top lit
next ;c
pool,

HardFault_exc handler
here b ;c

NMI_exc default-handler
lr bx ;c
