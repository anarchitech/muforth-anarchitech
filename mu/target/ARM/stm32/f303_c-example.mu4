( This file is part of muforth: https://muforth.nimblemachines.com/

  Copyright 2002-2022 David Frech. (Read the LICENSE for details.)

loading STM32F303xC example startup code

( Create a flash image containing a vector table, the ITC Forth kernel, and
  some basic startup code.)

__meta
hex

{ h @ } flash  ( save region and set to flash)
( Lay down a vector table, using end of ram as initial SP.)
0 vector-table flash-vectors
flash-vectors  ( make it current vector table)

{ h ! }  ( restore region pointer)
ld target/ARM/v6-m/kernel-itc.mu4

( Turn on HSE, which is driven by an 8 Mhz signal from the ST-LINK. Use it
  to drive the PLL to 72 Mhz - x9 - and when the PLL stabilizes, switch to
  it as the system clock source.)

: wait-for-rdy  ( regaddr)
   begin  dup c@  2 and  until  ( wait for RDY bit)  drop ;

: clock-init
   2 ( latency) FLASH_ACR set!  ( we need two cycles for 72 MHz clock!)
   5 ( HSEON + HSEBYP) [ RCC_CR 2 + #] tuck c!  wait-for-rdy ( wait for HSERDY)
   001d_0000 RCC_CFGR ! ( USBPRE=/1.5; PLLMUL=9; PLLSRC=HSE)
   1 ( PLLON)          [ RCC_CR 3 + #] tuck c!  wait-for-rdy ( wait for PLLRDY)
   2 ( SW=PLL as sysclk)  RCC_CFGR tuck c!
   begin  dup c@ %1010 =  until  ( SW=PLL and SWS=PLL)  drop ;

: wait   for next ;
: flash-leds
   -- Turn on clock to Port E
   ( Ports GFED_CBAH )
        [ %0010_0000  #16 << #] RCC_AHBENR set!
   -- Make PE8 to PE15 outputs
   5555_0000 GPIOE_MODER set!
   -- Flash!
   0000_ff00 dup GPIOE_BSRR !  3_0000 wait  GPIOE_BRR ! ;

here ]  ( chip-init)
   clock-init  flash-leds  begin again  [

Reset_exc handler
   -- Startup code!
   -- Since we have a Forth kernel, let's write the startup code in Forth!
   sp rp mov   #64 cells # sp sp sub  ( RP at end of RAM; SP below)
   ( chip-init)  ip  lit
   "decafbad     top lit
   next  ;c
   pool,

HardFault_exc handler
   here b  ;c  ( hang)

NMI_exc handler
default-handler
   lr bx  ;c  ( return to sender!!)


( NOTES about the f303 on the F3-Disco board.)

( LEDs are attached to PE8 to PE15.

  Port  Led  Dir  Color
-----------------------------
  PE8   LD4   NW  blue
  PE9   LD3   N   red
  PE10  LD5   NE  orange
  PE11  LD7   E   green
  PE12  LD9   SE  blue
  PE13  LD10  S   red
  PE14  LD8   SW  orange
  PE15  LD6   W   green

  ST-LINK is connected to USART1_RX and _TX; PC5 and PC4, resp.
  USART1 is AF7 for PC4 and PC5.
)
